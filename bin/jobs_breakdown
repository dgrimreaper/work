#!/usr/bin/perl

#use POSIX;
use Getopt::Long;
use Pod::Usage;
use Date::Format;
use strict;
use warnings;

my $startdate;
my $enddate;
my @date=localtime();
my $year=strftime("%Y",@date);
my $quarter=strftime("%q",@date);
my $user;

if ($quarter==1) {
        $startdate = "$year-01-01T00:00:00";
        $enddate = "$year-03-31T23:59:59";
    } elsif ($quarter==2) {
        $startdate = "$year-04-01T00:00:00";
        $enddate = "$year-06-30T23:59:59";
    } elsif ($quarter==3) {
        $startdate = "$year-07-01T00:00:00";
        $enddate = "$year-09-30T23:59:59";
    } elsif ($quarter==4) {
        $startdate = "$year-10-01T00:00:00";
        $enddate = "$year-12-31T23:59:59";
    } else { die "What kind of year has more than 4 quarters!" }

my $help = 0;
my @projects;
my $userproject = $ENV{'IVEC_PROJECT'};
my $cluster = $ENV{'IVEC_CLUSTER'};
my @groups = split /\s+/,`groups`;
my $mygroups = 0;

GetOptions ("project=s" => \@projects,          # Project to gather data for
    "cluster=s" => \$cluster,                   # Cluster to gather data for
    "mygroups" => \$mygroups,                   # Whether to print information about all groups the user is in
    "begin=s" => \$startdate,                   # Override start and end dates
    "end=s" => \$enddate,
    "user=s" => \$user,
    "help|?" => \$help,
);

pod2usage(1) if $help;

if (defined $userproject and length $userproject and scalar @projects==0) { push @projects, $userproject};
if ($mygroups) { @projects = @groups; }

die "Project not set, see help to specify project.\n" unless (scalar @projects>0);
die "Cluster not set, see help to specify cluster.\n" unless length $cluster;
my $project = join(',',@projects);
my $projectstr = '';
if ($project ne 'all') {$projectstr = "-A $project";};
my $userstr = '-a';
if (defined $user) {$userstr = "-u $user";};

sub toGB {
  my $num = $_[0];
  my $amount = 0;
  if ($num =~ s/[Gg].*//) { $amount = $num };
  if ($num =~ s/[Mm].*//) { $amount = $num/1000 };
  if ($num =~ s/[Kk].*//) { $amount = $num/1000000 };
  return $amount;
}

sub toHours {
  #[DD-[hh:]]mm:ss
  /(?:(\d+)-)?(?:(\d+):)?(\d+):(\d+)/;
  my $days=$1 // 0;
  my $hours=$2 // 0;
  my $minutes=$3 // 0;
  my $seconds=$4 // 0;
  my $answer=$days*24 + $hours + $minutes/60 + $seconds/3600;
  return $answer;
}

sub lbound {
  #[128-256) - returns 128
  $_[0] =~ /(\d+)-\d+/;
  return $1;
}

sub histogram {
  my $n = shift;
  my $histlow = "0";
  my $histhigh = "0";
  if ($n<1) {
    $histlow = "0";
    $histhigh = "0";
  } else {
    my $tmp = int(log($n) / log(2));
    $histlow = 2 ** ($tmp);
    $histhigh = 2 ** ($tmp+1);
  }
  return "[$histlow\-$histhigh)";
}

my %sizehist;
my %nodes;
my $nodehist;
my $memhist;
my %cputime;
my %elapsed;
my %usage;
my $totaljobs = 0;
my $totalcputime = 0;
my $totalusage = 0;

open(SACCT,"sacct -n -M $cluster $userstr $projectstr -P -S $startdate -E $enddate -T -X --format=jobid,user,account,reqcpus,alloccpus,nnodes,reqmem,MaxRSS,MaxVMSize,State,CPUTimeRAW,NCPUS,Elapsed |");
while(<SACCT>){
    next if /^[0-9]*\.batch/; # remove .batch but leave in job array components.
    chomp;
    my @fields=split /\|/;
    my $jobid     = $fields[0];
    my $node      = $fields[5];
    my $state     = $fields[9];
    next if $state =~ /PENDING/;
    my $size_req  = toGB($fields[6]);
    my $size_used = toGB($fields[7]);
    my $cputimeraw   = $fields[10];
    my $ncpus   = $fields[11];
    my $elapsed = $fields[12];
    my $elapsedH = toHours($elapsed);
    my $memhist = histogram($size_req);
    my $nodehist = histogram($node);
    $sizehist{$memhist} //= 0;
    $nodes{$nodehist} //= 0;
    $cputime{'bynodereq'}{$nodehist} //= 0;
    $cputime{'bynodereq'}{$nodehist} //= 0;
    $cputime{'bymemreq'}{$memhist} //= 0;
    $elapsed{'bymemreq'}{$memhist} //= 0;
    $elapsed{'bynodereq'}{$memhist} //= 0;
    $elapsed{'maxformemreq'}{$memhist} //= 0;
    $elapsed{'maxfornodereq'}{$nodehist} //= 0;
    $usage{'bynodereq'}{$nodehist} //= 0;
    $usage{'bymemreq'}{$memhist} //= 0;
    $totaljobs += 1;
    $sizehist{$memhist} += 1;
    $nodes{$nodehist} += 1;
    $totalcputime += $cputimeraw/3600;
    $cputime{'bynodereq'}{$nodehist} += $cputimeraw/3600;
    $cputime{'bymemreq'}{$memhist} += $cputimeraw/3600;
    $totalusage += $elapsedH * $node;
    if ($ncpus>0) {
      $elapsed{'bymemreq'}{$memhist} += $elapsedH;
      $elapsed{'bynodereq'}{$nodehist} += $elapsedH;
      $usage{'bymemreq'}{$memhist} += $elapsedH * $node;
      $usage{'bynodereq'}{$nodehist} += $elapsedH * $node;
      if ($elapsedH > $elapsed{'maxformemreq'}{$memhist}) {$elapsed{'maxformemreq'}{$memhist} = $elapsedH; }
      if ($elapsedH > $elapsed{'maxfornodereq'}{$nodehist}) {$elapsed{'maxfornodereq'}{$nodehist} = $elapsedH; }
    }
}
close SACCT;

print "Start: $startdate\n";
print "End: $enddate\n";
print "Cluster: $cluster\n";
print "Project(s): $project\n";
if (defined $user) {
  print "User: $user\n";
} else {
  print "User: all users\n";
}

$totaljobs || die "\nNo jobs in this period!!!\n\n";

######################################################################
my $cpuusage = 0;
my $cumjobs = 0;
my $cumusage = 0;
my $cumelapsed = 0;
my $maxelapsed = 0;

print "\nBy memory request\n";
printf "%12s %12s %8s %15s %8s %16s %8s %15s %15s\n","Request (GB)","Number","Cum %","Usage (coreH)","Cum %","Usage (nodeH)","Cum %","Elapsed(ave,H)","Elapsed(max,H)";
foreach my $size (sort {&lbound($a) <=> &lbound($b)} keys %sizehist) {
  $cumjobs += $sizehist{$size};
  $cumelapsed += $elapsed{'bymemreq'}{$size};
  $cumusage += $usage{'bymemreq'}{$size};
  $cpuusage += $cputime{'bymemreq'}{$size};
  if ($elapsed{'maxformemreq'}{$size} > $maxelapsed) {$maxelapsed = $elapsed{'maxformemreq'}{$size};}
  printf "%12s %12d %8.2f %15.2f %8.2f %16.2f %8.2f %15.2f %15.2f\n",$size,$sizehist{$size},100*$cumjobs/$totaljobs,$cputime{'bymemreq'}{$size},100*$cpuusage/$totalcputime,$usage{'bymemreq'}{$size},100*$cumusage/$totalusage,$elapsed{'bymemreq'}{$size}/$sizehist{$size},$elapsed{'maxformemreq'}{$size};
}
print "-" x 117; print "\n";
printf "%12s %12d %8.2f %15.2f %8.2f %16.2f %8.2f %15.2f %15.2f\n","Totals",$totaljobs,100*$cumjobs/$totaljobs,$cpuusage,100*$cpuusage/$totalcputime,$cumusage,100*$cumusage/$totalusage,$cumelapsed/$cumjobs,$maxelapsed;

######################################################################
$cpuusage = 0;
$cumjobs = 0;
$cumusage = 0;
$cumelapsed = 0;
$maxelapsed = 0;

print "\nBy node request\n";
printf "%12s %12s %8s %15s %8s %16s %8s %15s %15s\n","Nodes","Number","Cum %","Usage (coreH)","Cum %","Usage (nodeH)","Cum %","Elapsed(ave,H)","Elapsed(max,H)";
foreach my $node (sort {&lbound($a) <=> &lbound($b)} keys %nodes) {
  $cumjobs += $nodes{$node};
  $cumelapsed += $elapsed{'bynodereq'}{$node};
  $cumusage += $usage{'bynodereq'}{$node};
  $cpuusage += $cputime{'bynodereq'}{$node};
  if ($elapsed{'maxfornodereq'}{$node} > $maxelapsed) {$maxelapsed = $elapsed{'maxfornodereq'}{$node};}
  printf "%12s %12d %8.2f %15.2f %8.2f %16.2f %8.2f %15.2f %15.2f\n",$node,$nodes{$node},100*$cumjobs/$totaljobs,$cputime{'bynodereq'}{$node},100*$cpuusage/$totalcputime,$usage{'bynodereq'}{$node},100*$cumusage/$totalusage,$elapsed{'bynodereq'}{$node}/$nodes{$node},$elapsed{'maxfornodereq'}{$node};
}
print "-" x 117; print "\n";
printf "%12s %12d %8.2f %15.2f %8.2f %16.2f %8.2f %15.2f %15.2f\n","Totals",$totaljobs,100*$cumjobs/$totaljobs,$cpuusage,100*$cpuusage/$totalcputime,$cumusage,100*$cumusage/$totalusage,$cumelapsed/$cumjobs,$maxelapsed;

exit;

__END__

=head1 NAME

pawseyJobsBreakdown - Get Breakdown of Job Account Information From Pawsey Systems

=head1 SYNOPSIS

pawseyJobsBreakdown [options]

 Options:
   -project=id      specify project to query
   -cluster=name    specify cluster to query
   -mygroups        print information about all groups the user is in
   -begin=date      start date of query
   -end=date        end date of query
   -user=name       restrict query to one user
   -help            brief help message

=head1 OPTIONS

=over 8

=item B<-project>

Specify project ID that you wish to query the usage for. Defaults to IVEC_PROJECT environment variable.
Can be specified multiple times to sum over multiple projects.

=back

=item B<-cluster>

Specify cluster that you wish to query the usage for. Defaults to IVEC_CLUSTER environment variable.

=back

=item B<-mygroups>

Print out information for all groups the user is a member of.  This overrides the -projects option.

=back

=item B<-begin=s>

Set the start of the reporting period to the string "s".  Example format 2015-01-01T00:00:00.

=back

=item B<-end=s>

Set the end of the reporting period to the string "s".  Example format 2015-12-31T23:59:59.

=back

=item B<-user=s>

Restrict the query to a single user.

=back

=item B<-help>

Print a brief help message and exits.

=back


=head1 DESCRIPTION

B<pawseyJobsBreakdown> queries the Pawsey Supercomputing Centre systems for information about allocation statistics.

=cut

